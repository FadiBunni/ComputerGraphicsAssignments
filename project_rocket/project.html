<!DOCTYPE html>
<html>

<script id="vertex-shader-ground" type="x-shader/x-vertex">
attribute vec4 a_Position;
attribute vec2 a_TexCoord;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
varying vec2 fTexCoord;

uniform mat4 modelViewMatrixLight;
uniform mat4 projectionMatrixLight;
varying vec4 v_PositionFromLight;

void main() {
  gl_Position = projectionMatrix * modelViewMatrix * a_Position;
  fTexCoord = a_TexCoord;

  v_PositionFromLight = projectionMatrixLight * modelViewMatrixLight * a_Position;
}
</script>

<script id="fragment-shader-ground" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D textureMap;
uniform sampler2D shadowMap;

varying vec2 fTexCoord;

varying vec4 v_PositionFromLight;

void main() {
  vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
  vec4 rgbaDepth = texture2D(shadowMap, shadowCoord.xy);
  float depth = rgbaDepth.r;
  float visibility = (shadowCoord.z > depth + 0.01) ? 0.6 : 1.0;
  vec4 fColor = visibility * texture2D(textureMap, fTexCoord);
  fColor.a = 1.0;
  gl_FragColor = fColor;
}
</script>

<script id="vertex-shader-obj" type="x-shader/x-vertex">
precision mediump float;
attribute vec4 a_Position;
attribute vec4 a_Normal;
attribute vec4 a_Color;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

varying vec3 pos;
varying vec4 fNormal;
varying vec4 fColor;
void main() {
  fNormal = a_Normal;
  fColor = a_Color;

  pos = (modelViewMatrix * a_Position).xyz;

  gl_Position = projectionMatrix * modelViewMatrix * a_Position;
}
</script>

<script id="fragment-shader-obj" type="x-shader/x-fragment">
precision mediump float;
uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;
uniform vec4 lightPosition;
uniform mat4 modelViewMatrix;
uniform mat3 normalMatrix;

uniform float shininess;

varying vec3 pos;
varying vec4 fNormal;
varying vec4 fColor;
void main() {
	vec4 shading;
	vec3 light = (modelViewMatrix * lightPosition).xyz;
	vec3 L = lightPosition.w == 0.0 ? normalize(light) : normalize(light - pos);
	vec3 E =  -normalize(pos);
	vec3 N = normalize(normalMatrix*fNormal.xyz);
	vec3 H = normalize( L + E );
	vec4 ambient = ambientProduct;
	float Kd = max( dot(L, N), 0.0 );
	vec4  diffuse = Kd*diffuseProduct;
	float Ks = pow( max(dot(N, H), 0.0), shininess );
	vec4  specular = Ks * specularProduct;
	if( dot(L, N) < 0.0 ) {
	  specular = vec4(0.0, 0.0, 0.0, 1.0);
	}

	shading = ambient + diffuse + specular;
	shading.a = 1.0;
	gl_FragColor = fColor * shading;
}
</script>

<script id="vertex-shader-shadow" type="x-shader/x-vertex">
attribute vec4 a_Position;
uniform mat4 projectionMatrix;
uniform mat4 modelViewMatrix;
void main() {
  gl_Position = projectionMatrix * modelViewMatrix * a_Position;
}
</script>

<script id="fragment-shader-shadow" type="x-shader/x-fragment">
precision mediump float;

void main() {
  gl_FragColor = vec4(gl_FragCoord.z, 0.0, 0.0, 1.0);
}
</script>

<script type="text/javascript" src="./common/webgl-utils.js"></script>
<script type="text/javascript" src="./common/initshaders.js"></script>
<script type="text/javascript" src="./common/mv.js"></script>
<script type="text/javascript" src="./common/objparser.js"></script>
<script type="text/javascript" src="project.js"></script>

<body>
<canvas id="gl-canvas">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
<br/>
<button id = "lightRotation">Stop/Start Light Rotation</button>
<h3><a href="../index.html">Go back</a></h3>
</body>
</html>
